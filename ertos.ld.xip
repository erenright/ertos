ENTRY(_start)

C_STACK_SIZE	= 4096;
IRQ_STACK_SIZE	= 4096;
FIQ_STACK_SIZE	= 4096;
SVC_STACK_SIZE	= 4096;
ABT_STACK_SIZE	= 4096;
UND_STACK_SIZE	= 4096;

HEAP_SIZE	= 0x00100000;	/* 1MB heap */

ROM_START   = 0x01F20000;
RAM_START   = 0x00100000;

MEMORY
{
	rom (rx)    : org = 0x01F20000, len = 128k
	ram (rwx)   : org = 0x00100000, len = 15M
}

SECTIONS
{
	.text ROM_START : AT (0)
	{
		__text_start__ = . ;
		startup.o;
		*(.text);
		*(.rodata);

		. = ALIGN(4);
		__text_end__ = . ;
	} >rom

	.data RAM_START : AT ( SIZEOF(.text) )
	{
		__data_load__ = __text_end__;
		__data_start__ = . ;

		KEEP(*(.jcr))
		*(.got.plt) *(.got)
		*(.shdata)
		*(.data .data.* .gnu.linkonce.d.*)

		. = ALIGN(4);
		__data_end__ = . ;
	} >ram

	.bss RAM_START + SIZEOF(.data) :
	{
		__bss_start__ = .;
		*(.bss);

		. = ALIGN(4);
		__bss_end__ = .;
	} >ram

	.stack RAM_START + SIZEOF(.data) + SIZEOF(.bss) : {
		. = ALIGN(4);
		__stack_start__ = .;

		. += IRQ_STACK_SIZE;
		. = ALIGN(4);
		__irq_stack_top__ = .;

		. += FIQ_STACK_SIZE;
		. = ALIGN(4);
		__fiq_stack_top__ = .;

		. += SVC_STACK_SIZE;
		. = ALIGN(4);
		__svc_stack_top__ = .;

		. += ABT_STACK_SIZE;
		. = ALIGN(4);
		__abt_stack_top__ = .;

		. += UND_STACK_SIZE;
		. = ALIGN(4);
		__und_stack_top__ = .;

		. += C_STACK_SIZE;
		. = ALIGN(4);
		__c_stack_top__ = .;

		__stack_end__ = .;
	}

	__end__ = .;
}
